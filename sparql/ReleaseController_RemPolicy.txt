PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX osph: <http://www.ontoeng.com/osph#>
PREFIX cs: <http://www.ontoeng.com/controlSystem#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifcext: <http://www.ontoeng.com/IFC4_ADD1_extension#>
PREFIX factory: <http://www.ontoeng.com/factory#>

DELETE{
	GRAPH ?g {
		?condTrIdleRel osph:hasConditionExpression ?condexpr .
	}
}
WHERE {

	?controller osph:hasStateMachine ?stM .
	?stM fsm:contains ?region .
	?region fsm:hasStateMachineElement ?idleSt .
	?region fsm:hasStateMachineElement ?relSt .		
	?idleSt rdf:type cs:RControllerIdleState .
	?relSt rdf:type cs:RControllerReleaseState .
	
	?trIdleRel fsm:Source ?idleSt .
	
	?relSt fsm:Entry/osph:generatesEvent ?startOp .
	?eventType ifcext:typesObject|ifcext:declaresObject ?startOp .
	
	?trIdleRel fsm:TransitionGuard/fsm:GuardCondition ?condTrIdleRel .
	GRAPH ?g {
		?condTrIdleRel osph:hasConditionExpression ?condexpr .
	}
 	?machine osph:hasStateMachine/fsm:contains/fsm:hasStateMachineElement*/fsm:GuardEvent ?eventType .
	{?eventType rdf:type factory:MachineToolStartOperationType .}
	UNION {?eventType rdf:type factory:MachineToolStartOperation .}
}