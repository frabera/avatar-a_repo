PREFIX osph: <http://www.ontoeng.com/osph#>
PREFIX fsm: <http://www.learninglab.de/~dolog/fsm/fsm.owl#>
PREFIX ifcext: <http://www.ontoeng.com/IFC4_ADD1_extension#>
PREFIX cs: <http://www.ontoeng.com/controlSystem#>

SELECT distinct ?controlledMach ?region ?controlPolicy

WHERE { 
  # The selected controller is a user-defined parameter
  <#controller#> osph:hasStateMachine ?stM .
  ?stM fsm:contains ?region .
  ?region fsm:hasStateMachineElement ?relState .
  ?relState fsm:Entry/osph:generatesEvent ?event .
  ?controlledMach osph:hasStateMachine/fsm:contains/fsm:GuardEvent/ifcext:typesObject ?event .
  ?relTransition fsm:Target ?relState .
  OPTIONAL{
    ?relTransition fsm:TransitionGuard/fsm:GuardCondition/osph:hasConditionExpression ?condexpr .
    ?controlPolicy cs:hasPolicyExpression ?condexpr .
  }
}