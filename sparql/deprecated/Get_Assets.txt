PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX list: <https://w3id.org/list#> 
PREFIX express: <https://w3id.org/express#> 
PREFIX ifc: <http://ifcowl.openbimstandards.org/IFC4_ADD1#> 
PREFIX osph: <http://www.ontoeng.com/osph#>
PREFIX ifcext: <http://www.ontoeng.com/IFC4_ADD1_extension#>


SELECT distinct ?asset ?itsType ?itsDescr ?itsModel ?reprLUp ?reprLoc ?itsPos ?itsRot ?itsRelTo ?itsParent ?itsBuilding


WHERE { 
  {?asset rdf:type/rdfs:subClassOf* ifc:IfcProduct .
   OPTIONAL{ ?asset ifc:representation_IfcProduct/ifc:representations_IfcProductRepresentation/list:hasContents/osph:hasExternalRepresentation ?extrepr .  
     OPTIONAL{?extrepr osph:hasRepresentationLengthUnit/ifc:prefix_IfcSIUnit ?reprLUp .}
     ?extrepr ifc:location_IfcExternalReference/express:hasString ?reprLoc . 
	 }
  } UNION {?asset rdf:type/rdfs:subClassOf* ifc:IfcTypeProduct .
   OPTIONAL{ ?asset ifc:representationMaps_IfcTypeProduct/list:hasContents/ifc:mappedRepresentation_IfcRepresentationMap/osph:hasExternalRepresentation ?extrepr .
       OPTIONAL{?extrepr osph:hasRepresentationLengthUnit/ifc:prefix_IfcSIUnit ?reprLUp .}
      ?extrepr ifc:location_IfcExternalReference/express:hasString ?reprLoc .   	
    }
  }
    ?asset rdf:type ?itsType .
    FILTER ( ?itsType != owl:NamedIndividual ) . 
	OPTIONAL { ?asset ifc:description_IfcRoot/express:hasString ?itsDescr } .
    OPTIONAL { ?asset ifcext:isDefinedByType|^ifcext:typesObject ?itsModel} .
    OPTIONAL { ?asset (ifcext:isContainedInStructure|^ifcext:containsProduct) ?itsBuilding } .
    OPTIONAL { ?asset (ifcext:decomposesObject|^ifcext:isDecomposedByObject) ?itsParent } .
  OPTIONAL{ ?asset ifc:objectPlacement_IfcProduct ?objplace . 
    OPTIONAL{
      ?objplace ifc:relativePlacement_IfcLocalPlacement ?relplace .
		OPTIONAL{ ?relplace ifcext:hasRotationMatrix ?itsRot . }
		OPTIONAL{ ?relplace ifcext:hasEulerAnglesYXZ ?itsRot . }
		OPTIONAL{ ?relplace ifcext:hasQuaternion ?itsRot . }
    	OPTIONAL{ ?relplace ifcext:hasLocation ?itsPos . }
    }
    OPTIONAL{ 
      	?objplace ifc:placementRelTo_IfcLocalPlacement/^ifc:objectPlacement_IfcProduct ?itsRelTo .
    }
  }
}